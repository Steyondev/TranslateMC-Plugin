plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'maven-publish'
}

// Automatic versioning from environment variable or Git
def getVersion() {
    // First, try to get VERSION from environment (set by CI/CD)
    def envVersion = System.getenv('VERSION')
    if (envVersion && !envVersion.isEmpty()) {
        println "Using VERSION from environment: ${envVersion}"
        return envVersion
    }
    
    // Fallback to Git tags
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--always', '--dirty'
            standardOutput = stdout
            errorOutput = new ByteArrayOutputStream()
        }
        def gitVersion = stdout.toString().trim()
        println "Using VERSION from Git: ${gitVersion}"
        
        // Remove 'v' prefix if present (e.g., v1.0.1 -> 1.0.1)
        if (gitVersion.startsWith('v')) {
            return gitVersion.substring(1)
        }
        return gitVersion
    } catch (Exception e) {
        println "Warning: Could not determine version from Git. Using default version."
        return '1.0.0-dev'
    }
}

group = 'dev.steyon'
version = getVersion()

println "Building TranslateMC-Plugin version: ${version}"

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/releases/'
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT")
    compileOnly 'me.clip:placeholderapi:2.11.7'
    implementation 'com.google.code.gson:gson:2.10.1'
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    repositories {
        maven {
            name = "github"
            url = uri("https://maven.pkg.github.com/steyondev/TranslateMC-Plugin")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
